# Maintainer: Myles English < myles at rockhead dot biz >
# Contributer: Sebastian Eiser

pkgname=scotch_esmumps
pkgver=6.0.0
pkgrel=2
pkgdesc="libraries for graph, mesh and hypergraph partitioning, static mapping, and sparse matrix block ordering. This version  contains an interface for MUMPS. The sequential and parallel version are built"
url="http://www.labri.fr/perso/pelegrin/scotch/"
license="CeCILL-C free/libre software license"
depends=('zlib' 'openmpi')
makedepends=('ed')
provides=('ptscotch' 'scotch')
conflicts=('ptscotch' 'scotch' 'ptscotch-openmpi' 'ptscotch-mpich2')
arch=('any')
install=${pkgname}.install
options=('!makeflags')
source=("http://gforge.inria.fr/frs/download.php/31832/scotch_${pkgver}_esmumps.tar.gz")
sha256sums=('8206127d038bda868dda5c5a7f60ef8224f2e368298fbb01bf13fa250e378dd4')

prepare() {
  cd "${srcdir}/scotch_${pkgver}_esmumps"/src

  cp Make.inc/Makefile.inc.i686_pc_linux2.shlib Makefile.inc
  sed -i "s/-O3/${CFLAGS}/g" Makefile.inc
}

build() {
  cd "${srcdir}/scotch_${pkgver}_esmumps"/src

  make scotch
  make ptscotch
  make esmumps
  make ptesmumps
}

package() {
  cd "${srcdir}/scotch_${pkgver}_esmumps"

  install -d "$pkgdir/usr"
  make -C src prefix=$pkgdir/usr install

  install -m644 lib/libesmumps.so lib/libptesmumps.so "$pkgdir/usr/lib"
  install -m644 include/esmumps.h "$pkgdir/usr/include"
  install -Dm644 doc/CeCILL-C_V1-en.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
