# Maintainer: jedbrown
# Contributor: mickele
# Contributor: gborzi
# Contributor: abenson

pkgname=metis
pkgver=5.1.0
pkgrel=1
pkgdesc='A set of serial programs for partitioning graphs, partitioning finite element meshes, and producing fill reducing orderings for sparse matrices.'
url="http://glaros.dtc.umn.edu/gkhome/metis/metis/overview"
license=('custom')
arch=('i686' 'x86_64')
makedepends=('cmake')
install="$pkgname.install"
source=("http://glaros.dtc.umn.edu/gkhome/fetch/sw/metis/metis-$pkgver.tar.gz")
sha256sums=('76faebe03f6c963127dbb73c13eab58c9a3faeae48779f049066a21c087c5db2')

build() {
  cd "$pkgname-$pkgver"

  make config shared=1 prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make install DESTDIR=${pkgdir}

  # doc and license
  install -Dm644 manual/manual.pdf "$pkgdir/usr/share/doc/$pkgname/manual.pdf"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
