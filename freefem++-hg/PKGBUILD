# $Id$

pkgname=freefem++-hg
pkgver=3.35.dist+93+af6b74e90d77
pkgrel=1
pkgdesc='A PDE oriented language using the finite element method (Mercurial)'
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse')
makedepends=('ed' 'mercurial' 'gcc-fortran')
provides=('freefem++')
conflicts=('freefem++')
backup=('etc/freefem++.pref')
source=('hg+http://www.freefem.org/ff++/ff++'
        'configure-dirs.patch')
sha256sums=('SKIP'
            'f0f3274cc17f78f767eaf0db06be0a51c233c95de2421d6012206e401186b419')

pkgver() {
  cd ff++
  hg log -r. --template "{sub('-', '.', latesttag)}+{latesttagdistance}+{node|short}"
}

prepare() {
  cd ff++

  # no download pls
  ed -s Makefile.am <<< $'9s/download//\nw'

  # control install locations with ./configure
  patch -p1 < ../configure-dirs.patch
}

build() {
  cd ff++

  autoreconf -vi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-silent-rules \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd -lamd"

  make
}

# check() {
#   cd 'ff++'
#   # Some tests fail. See README_HG
#   make check
# }

package() {
  cd ff++

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f $pkgdir/usr/share/freefem++/3.32/INSTALL*
  rm -f $pkgdir/usr/share/freefem++/3.32/README_*
  rm -f $pkgdir/usr/share/freefem++/3.32/mode-mi-edp.zip
  rm -rf $pkgdir/usr/share/freefem++/3.32/download
}
