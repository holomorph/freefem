# $Id$

pkgname=freefem++-hg
pkgver=3.31+1+794839c2a7cd
pkgrel=1
pkgdesc='A PDE oriented language using the finite element method (Mercurial)'
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse')
makedepends=('ed' 'mercurial' 'gcc-fortran')
provides=('freefem++')
conflicts=('freefem++')
backup=('etc/freefem++.pref')
source=('hg+http://www.freefem.org/ff++/ff++'
        'disable-doc.patch'
        'configure-dirs.patch')
sha256sums=('SKIP'
            '4e01551ae3f06a34816c6e87d0b2a3636096072bc51a3e8a1e05542577e10e60'
            'c760a540447e08672b1e30174c27a7cebb4f8fbcee8eb8032ed219009e98303d')

pkgver() {
  cd 'ff++'

  # 2937 v3.27
  # 2944 v3.28
  # 2960 v3.29
  # 2999 v3.30
  # 3036 v3.30

  # make the tag and refer to the changeset prior to the tag
  hg tag -r3036 3.31
  hg log -r-2 --template "{sub('-', '.', latesttag)}+{latesttagdistance}+{node|short}"
}

prepare() {
  cd 'ff++'

  # disable doc
  patch -p1 < "$srcdir/disable-doc.patch"

  # control install locations with ./configure
  patch -p1 < "$srcdir/configure-dirs.patch"

  # enable subdirs (exacerbates issue with eigenvalue.o)
  # patch -p1 < "$srcdir/subdirs.patch"

  # no download pls
  ed -s Makefile.am <<< $'9s/download//\nw'

  # correct metis.h location
  ed -s configure.ac <<< $',s/metis\///g\nw'

  # correct superlu headers
  ed -s examples++-load/SuperLu.cpp <<< $',s/"slu_ddefs.h"/<superlu\/slu_ddefs.h>/g\nw'
  ed -s examples++-load/SuperLu.cpp <<< $',s/"slu_zdefs.h"/<superlu\/slu_zdefs.h>/g\nw'
  ed -s examples++-load/SuperLu.cpp <<< $',s/"superlu_enum_consts.h"/<superlu\/superlu_enum_consts.h>/g\nw'
}

build() {
  cd 'ff++'

  autoreconf -vi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"

  make
}

# check() {
#   cd 'ff++'
#   # Some tests fail. See README_HG
#   make check
# }

package() {
  cd 'ff++'

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f $pkgdir/usr/share/freefem++/3.31/INSTALL*
  rm -f $pkgdir/usr/share/freefem++/3.31/README_*
  rm -f $pkgdir/usr/share/freefem++/3.31/mode-mi-edp.zip
  rm -rf $pkgdir/usr/share/freefem++/3.31/download
}

# vim: ts=2 sw=2 et
