# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>
# Contributor: 'lazork'

pkgname=freefem++
pkgver=3.38+1
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse')
makedepends=('ed' 'gcc-fortran')
backup=('etc/freefem++.pref')
source=("http://www.freefem.org/ff++/ftp/$pkgname-${pkgver//+/-}.tar.gz"
        'disable-doc.patch'
        'configure-dirs.patch'
        'umfpack.patch')
sha256sums=('7bc126a16b3a437e9fe6e4ce52722eb7b6ec40752f23b71a926d9b86f9f2fbad'
            '19ba064469b9c38278fa1e396c0bcd1bb5d067a18eeaef980c86aff599881fdb'
            '0aef5b127176d4a49d9c034649f6e42e85dc31c6af85aae3f54d9d39cdd99ab0'
            'b097abbf87b15404416b473ccbcf6aa2054064ce6258db9340a36c34024d1346')

prepare() {
  cd "$pkgname-${pkgver//+/-}"

  # no download pls
  ed -v Makefile.in <<< $',s/SUBDIRS\ =\ download/SUBDIRS\ =\ /\nw'

  # disable doc
  patch -p1 < ../disable-doc.patch

  # control install locations with ./configure
  patch -p1 < ../configure-dirs.patch

  # correct headers
  # patch -p1 < ../umfpack.patch
}

build() {
  cd "$pkgname-${pkgver//+/-}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-silent-rules \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"

  make
}

package() {
  cd "$pkgname-${pkgver//+/-}"

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/INSTALL*
  rm -f "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/README_*
  rm -f "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/mode-mi-edp.zip
  rm -rf "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/download
}
