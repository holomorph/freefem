# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>
# Contributor: 'lazork'

pkgname=freefem++
pkgver=3.31
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse')
makedepends=('ed' 'gcc-fortran')
backup=('etc/freefem++.pref')
options=('staticlibs')
source=("http://www.freefem.org/ff++/ftp/${pkgname}-${pkgver}.tar.gz"
        'disable-doc.patch'
        'configure-dirs.patch')
sha256sums=('4eb36abbdd25e438d6c80beaa5e18d9e70b6774eafe484e8baf56412ea3b30a4'
            '19ba064469b9c38278fa1e396c0bcd1bb5d067a18eeaef980c86aff599881fdb'
            '6ddf4fd23390862707d3c0ce18fef937004a4e461ae7044ec940ceaff2616cea')

prepare() {
  cd "$pkgname-$pkgver"

  # correct metis.h location
  ed -s configure <<< $',s/metis\///g\nw'

  # correct superlu headers
  ed -s examples++-load/SuperLu.cpp <<< $',s/"slu_ddefs.h"/<superlu\/slu_ddefs.h>/g\nw'
  ed -s examples++-load/SuperLu.cpp <<< $',s/"slu_zdefs.h"/<superlu\/slu_zdefs.h>/g\nw'
  ed -s examples++-load/SuperLu.cpp <<< $',s/"superlu_enum_consts.h"/<superlu\/superlu_enum_consts.h>/g\nw'

  # disable doc
  patch -p1 < "$srcdir/disable-doc.patch"

  # control install locations with ./configure
  patch -p1 < "$srcdir/configure-dirs.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f $pkgdir/usr/share/$pkgname/INSTALL*
  rm -f $pkgdir/usr/share/$pkgname/README_*
  rm -f $pkgdir/usr/share/$pkgname/mode-mi-edp.zip
  rm -rf $pkgdir/usr/share/$pkgname/download
}

# vim: ts=2 sw=2 et
