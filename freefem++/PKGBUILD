# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>
# Contributor: 'lazork'

pkgname=freefem++
pkgver=3.34+1
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse')
makedepends=('ed' 'gcc-fortran')
backup=('etc/freefem++.pref')
source=("http://www.freefem.org/ff++/ftp/$pkgname-${pkgver//+/-}.tar.gz"
        'disable-doc.patch'
        'configure-dirs.patch'
        'umfpack.patch')
sha256sums=('a7fc76f23866983a42d0997bf75323e8d92f637668e40f7b1b4dc99a8d52bdfd'
            '19ba064469b9c38278fa1e396c0bcd1bb5d067a18eeaef980c86aff599881fdb'
            '531d1b0b112a89f30ef645d5533717cb09dcad6fecb88d73e87efb5514a4ff07'
            'b097abbf87b15404416b473ccbcf6aa2054064ce6258db9340a36c34024d1346')

prepare() {
  cd "$pkgname-${pkgver//+/-}"

  # no download pls
  ed -v Makefile.in <<< $',s/SUBDIRS\ =\ download/SUBDIRS\ =\ /\nw'

  # disable doc
  patch -p1 < ../disable-doc.patch

  # control install locations with ./configure
  patch -p1 < ../configure-dirs.patch

  # correct headers
  # patch -p1 < ../umfpack.patch
}

build() {
  cd "$pkgname-${pkgver//+/-}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-silent-rules \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"

  make
}

package() {
  cd "$pkgname-${pkgver//+/-}"

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/INSTALL*
  rm -f "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/README_*
  rm -f "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/mode-mi-edp.zip
  rm -rf "$pkgdir"/usr/share/"$pkgname/${pkgver//+/-}"/download
}
