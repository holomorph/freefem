# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>
# Contributor: 'lazork'

pkgname=freefem++
pkgver=3.31+3
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse')
makedepends=('ed' 'gcc-fortran')
backup=('etc/freefem++.pref')
options=('staticlibs')
source=("http://www.freefem.org/ff++/ftp/$pkgname-${pkgver//+/-}.tar.gz"
        'disable-doc.patch'
        'configure-dirs.patch'
        'umfpack.patch')
sha256sums=('1e915c58b2cf205274ea56730444448a89ba5a75bd51a91458d04ef77b929763'
            '19ba064469b9c38278fa1e396c0bcd1bb5d067a18eeaef980c86aff599881fdb'
            '6ddf4fd23390862707d3c0ce18fef937004a4e461ae7044ec940ceaff2616cea'
            'b097abbf87b15404416b473ccbcf6aa2054064ce6258db9340a36c34024d1346')

prepare() {
  cd "$pkgname-${pkgver//+/-}"

  # no download pls
  ed -v Makefile.in <<< $'434s/download//\nw'

  # disable doc
  patch -p1 < ../disable-doc.patch

  # control install locations with ./configure
  patch -p1 < ../configure-dirs.patch

  # correct headers
  patch -p1 < ../umfpack.patch
}

build() {
  cd "$pkgname-${pkgver//+/-}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"

  make
}

package() {
  cd "$pkgname-${pkgver//+/-}"

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f $pkgdir/usr/share/$pkgname/INSTALL*
  rm -f $pkgdir/usr/share/$pkgname/README_*
  rm -f $pkgdir/usr/share/$pkgname/mode-mi-edp.zip
  rm -rf $pkgdir/usr/share/$pkgname/download
}
