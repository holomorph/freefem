# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>
# Contributor: 'lazork'

pkgname=freefem++
pkgver=3.26
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse')
makedepends=('ed' 'gcc-fortran')
backup=('etc/freefem++.pref')
source=("http://www.freefem.org/ff++/ftp/${pkgname}-${pkgver}.tar.gz"
        'disable-doc.patch'
        'configure-dirs.patch')
sha256sums=('a0528c3847852ecc6e10e3b38e321d55f17702292ce7b8c3fb783e6cd357c120'
            '19ba064469b9c38278fa1e396c0bcd1bb5d067a18eeaef980c86aff599881fdb'
            '3e71a84310be143e0edf845b5f5f62f762e1e769c414acbcafadeb4a803856b2')

prepare() {
  cd "$pkgname-$pkgver"

  # correct metis.h location
  ed -s configure <<< $',s/metis\///g\nw'

  # correct superlu headers
  ed -s examples++-load/SuperLu.cpp <<< $',s/"slu_ddefs.h"/<superlu\/slu_ddefs.h>/g\nw'
  ed -s examples++-load/SuperLu.cpp <<< $',s/"slu_zdefs.h"/<superlu\/slu_zdefs.h>/g\nw'
  ed -s examples++-load/SuperLu.cpp <<< $',s/"superlu_enum_consts.h"/<superlu\/superlu_enum_consts.h>/g\nw'

  # disable doc
  patch -p1 < "$srcdir/disable-doc.patch"

  # control install locations with ./configure
  patch -p1 < "$srcdir/configure-dirs.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f $pkgdir/usr/share/$pkgname/INSTALL*
  rm -f $pkgdir/usr/share/$pkgname/README_*
  rm -f $pkgdir/usr/share/$pkgname/mode-mi-edp.zip
  rm -rf $pkgdir/usr/share/$pkgname/download
}

# vim: ts=2 sw=2 et
